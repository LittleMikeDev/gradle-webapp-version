plugins {
    id "com.gradle.plugin-publish" version "0.9.0"
}

apply plugin: 'groovy'
apply plugin: "jacoco"

sourceCompatibility = 1.6

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile group: 'gradle.plugin.uk.co.littlemike.build.version', name: 'build-version-plugin', version: '0.5'
}

pluginBundle {
    website = 'https://github.com/LittleMikeDev/gradle-webapp-version'
    vcsUrl = 'https://github.com/LittleMikeDev/gradle-webapp-version.git'
    description = 'Plugin for baking a static version page into a webapp'
    tags = ['webapp', 'version', 'json']

    plugins {
        webappVersionPlugin {
            id = 'uk.co.littlemike.webapp-version-plugin'
            displayName = 'Gradle webapp version plugin'
        }
    }
}

/**
 * Hack to allow plugin publish key to be specified as an environment variable in Travis-CI
 * Taken from: https://discuss.gradle.org/t/add-apikey-and-apisecret-to-pluginbundle-extension-for-plugin-publish-plugin/8636/4
 * This can be removed when https://issues.gradle.org/browse/GRADLE-3273 is resolved
 */
task setupPluginUpload << {
    def key = System.getenv('gradle.publish.key')
    def secret = System.getenv('gradle.publish.secret')

    if( !key || !secret) {
        throw new RuntimeException("gradlePublishKey and/or gradlePublishSecret are not defined environment variables")
    }

    System.properties.setProperty("gradle.publish.key", key)
    System.properties.setProperty("gradle.publish.secret", secret)
}
tasks.publishPlugins.dependsOn tasks.setupPluginUpload

jacocoTestReport {
    reports {
        xml.enabled true
    }
}